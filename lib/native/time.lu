export Date, TimeVal, TimeZone, TimeSpec

import FFI from std.ffi

FFI cdef """
   typedef long time_t;
   typedef unsigned long clock_t;

   typedef struct TimeSpec {
     time_t tv_sec;
     long   tv_nsec;
   } timespec_t;

   typedef struct Date {
     int tm_sec;	/* seconds after the minute [0-60] */
     int tm_min;	/* minutes after the hour [0-59] */
     int tm_hour;	/* hours since midnight [0-23] */
     int tm_mday;	/* day of the month [1-31] */
     int tm_mon;	/* months since January [0-11] */
     int tm_year;	/* years since 1900 */
     int tm_wday;	/* days since Sunday [0-6] */
     int tm_yday;	/* days since January 1 [0-365] */
     int tm_isdst;	/* Daylight Savings Time flag */
     long tm_gmtoff;	/* offset from CUT in seconds */
     char *tm_zone;	/* timezone abbreviation */
   } tm_t;

   typedef struct TimeVal {
      time_t      tv_sec;   /* seconds since Jan. 1, 1970 */
      suseconds_t tv_usec;  /* and microseconds */
   } timeval_t;

   typedef struct TimeZone {
      int tz_minuteswest; /* of Greenwich */
      int tz_dsttime;     /* type of dst correction to apply */
   } timezone_t;

   time_t time(time_t *tloc);
   int gettimeofday(timeval_t *tp, timezone_t *tzp);
   size_t strftime(char *, size_t, const char *, tm_t *);

   char *asctime(tm_t *);
   clock_t clock(void);
   char *ctime(time_t *);
   double difftime(time_t, time_t);
   tm_t *getdate(const char *);
   tm_t *gmtime(time_t *);
   tm_t *localtime(time_t *);
   tm_t *localtime_r(time_t *clock, tm_t *result);
   time_t mktime(tm_t *);

"""

class TimeVal with FFI::MetaType['timeval_t'] {
   method sec  { self::tv_sec  }
   method usec { self::tv_usec }
   method init(time = 0.0) {
      if time {
         var integ, fract = math::modf(tonumber(time))
         self::tv_sec  = integ
         self::tv_usec = fract * 1000000
      }
   }
   method toDate { Date.new(self.sec) }
}

class TimeZone with FFI::MetaType['timezone_t'] {
   method minuteswest { self::tz_minuteswest }
   method dsttime     { self::tz_dsttime     }
}

class TimeSpec with FFI::MetaType['timespec_t'] {
   method sec  { self::tv_sec  }
   method nsec { self::tv_nsec }

   method toDate { Date.new(self.sec) }
}

class Date with FFI::MetaType['tm_t'] {

   var C = FFI::C

   static method getTimeOfDay {
      var tv = TimeVal.new
      var tz = TimeZone.new
      var ok = C::gettimeofday(tv, tz)
      if ok == 0 {
         return tv, tz
      }
   }

   method init(time = C::time(FFI.new('time_t[1]'))) {
      if !FFI.istype('time_t*', time) {
         var temp = FFI.new('time_t[1]')
         temp::[0] = time
         time = temp
      }
      C::localtime_r(time, self)
   }

   method format(fmt : String, len : Number = 256) {
      var buf = FFI.new('char[?]', len)
      C::strftime(buf, len, fmt, self)
      return FFI.string(buf)
   }
}


